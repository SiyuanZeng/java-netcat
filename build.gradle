apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

sourceCompatibility = 1.8
version = '1.1'

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDir 'src'

        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

dependencies {
    compile 'args4j:args4j:2.0.29'
    testCompile 'junit:junit:4.11'
}

mainClassName = 'com.github.dddpaul.netcat.NetCat'

// After executing "jar" task one can run program like this:
// java -jar build/libs/java-netcat-{version}.jar -p 9999 -l        - listen
// java -jar build/libs/java-netcat-{version}.jar -p 9999 127.0.0.1 - connect
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest.attributes("Main-Class": mainClassName)
}

// Using "run" task one can run program like this:
// gradle run -PappArgs="['-p','9999','-l']"        - listen
// gradle run -PappArgs="['-p','9999','127.0.0.1']" - connect
run {
    if(project.hasProperty('appArgs')) {
        args Eval.me(appArgs)
    }
}
